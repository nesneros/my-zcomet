_gen-comp() {
    cmd=$2
    destFile="$1/_$cmd"
    hash "$cmd" &>/dev/null || return
    echo "completion ${cmd} -> ${destFile}"
    $cmd completion zsh >"$destFile"
}

_upgrade-completions() {
    destDir="$MY_ZCOMET/functions/generated"
    mkdir -p "$destDir"
    _gen-comp "$destDir" jira
    _gen-comp "$destDir" kubectl
    _gen-comp "$destDir" orbctl
}

{
    # Silence job info
    setopt LOCAL_OPTIONS NO_NOTIFY NO_MONITOR

    if [[ -n "$ZCOMET_SOURCE" ]]; then
        source "$ZCOMET_SOURCE"
        # print "\n--- Upgrade zcomet ---"
        ({
            zcomet self-update
            zcomet update
        } 2>&1 |
            sed -u 's/\x1B\[[0-9;]\{1,\}[A-Za-z]//g' |
            grep --line-buffered -v "^\(Already up to date\.\|You may now run .exec zsh' to refresh the system\.\|zcomet: Self-update finished\.\|$\)$" | sed -u "s/^/zcomet: /") # &
    fi

    if hash apt-get &>/dev/null; then
        print "\n--- Upgrading APT packages ---"
        sudo apt-get update && sudo apt-get upgrade
    fi

    if hash brew &>/dev/null; then
        print "\n--- Upgrading brews ---"
        # brew upgrade --cask --greedy
        brew upgrade --cask
        brew upgrade
        brew cleanup
        print -- "--- Done upgrading brews ---\n"
    fi

    if hash mise &>/dev/null; then
        print "\n--- Upgrading mise ---"
        mise upgrade 2>&1
        if ! mise dr >/dev/null 2>&1; then
            mise dr || :
        fi
        print -- "--- Done upgrading mise ---\n"
    fi

    if hash kubectl &>/dev/null && [ -d "$HOME"/.krew/bin ]; then
        #print "\n--- Upgrading kubectl krew ---"
        (kubectl krew upgrade 2>&1 | grep --line-buffered -v "it is already on the newest version$" | sed -u 's/^/kubectl krew: /') # &
    fi

    # print "\n--- Upgrade completions ---"
    _upgrade-completions # &
    # wait
}

exec zsh
